
'===============================================================================
' 構造体
'===============================================================================
Type IssueArgs
'-------------------------------------------------------------------------------
' CreateIssueの引数オブジェクト
'-------------------------------------------------------------------------------
   project_id       As String
   tracker_id       As String
   status_id        As String
   priority_id      As String
   subject          As String
   description      As String
   category_id      As String
   fixed_version_id As String
   assigned_to_id   As String
   parent_issue_id  As String
   custom_fields    As String
   watcher_user_ids As String
   is_private       As String
   estimated_hours  As String
End Type

'===============================================================================
' サブルーチン
'===============================================================================
Sub ExecuteGetProjects()
'-------------------------------------------------------------------------------
' プロジェクトの取得を実行
'-------------------------------------------------------------------------------
   Dim url As String
   Dim key As String
   
   Call GetURLandAPIKey(url, key)
   Call GetProjects("Projects", 3, 1, url, key)
End Sub

Sub ExecuteGetTrackers()
'-------------------------------------------------------------------------------
' トラッカーの取得を実行
'-------------------------------------------------------------------------------
   Dim url As String
   Dim key As String

   Call GetURLandAPIKey(url, key)
   Call GetTrackers("Trackers", 3, 1, url, key)
End Sub

Sub ExecuteGetProjectMenberships()
'-------------------------------------------------------------------------------
' プロジェクトのメンバーの取得を実行
'-------------------------------------------------------------------------------
   Dim url As String
   Dim key As String

   Call GetURLandAPIKey(url, key)
   Call GetProjectMenberships("Users", 3, 1, url, key, "1")
End Sub

Sub ExecuteCreateIssues()
'-------------------------------------------------------------------------------
' チケットの作成を実行
'-------------------------------------------------------------------------------
   Dim url As String
   Dim key As String

   Call GetURLandAPIKey(url, key)
   Call CreateIssues("Issues", 3, 1, url, key)
End Sub


'===============================================================================
' 関数
'===============================================================================
Public Function GetURLandAPIKey(ByRef url As String, ByVal key As String)
'-------------------------------------------------------------------------------
' ParamsシートにあるURLとAPIKeyを取得
'-------------------------------------------------------------------------------
   url = Worksheets("Params").Range("C5").Value

   'apikey.txtがExcelファイルと同じフォルダにある場合はそこから取得
   On Error GoTo ERROR
   Open ThisWorkbook.Path & "\apikey.txt" For Input As #1
      Line Input #1, key
   Close
   Exit Function
ERROR:
   key = Worksheets("Params").Range("C6").Value

End Function

Public Function GetProjects(ByVal sheet As String, _
                            ByVal row As Long, _
                            ByVal col As Long, _
                            ByVal url As String, _
                            ByVal key As String, _
                            Optional hasHeader As Boolean = false)
'-------------------------------------------------------------------------------
' プロジェクトの情報をsheetの(row,col)を起点にして出力
'-------------------------------------------------------------------------------
   Dim total_count As Long
   Dim limit As Long
   Dim page As Long
   Dim xmlHttp As New MSXML2.XMLHTTP60
   Dim doc As New DOMDocument60
   
   With xmlHttp
      'total_countを取得してループ回数決める
      .Open "GET", url & "projects.xml?limit=1", False
      .setRequestHeader "Content-Type", "text/xml"
      .setRequestHeader "X-Redmine-API-Key", key
      .send
      If .Status = 200 Then
         'XMLデータを取り込む
         doc.LoadXML (.responseText)
         total_count = doc.SelectSingleNode("projects/@total_count").Text
      Else
         MsgBox .responseText
         GoTo Finally
      End If

      If hasHeader Then
         Worksheets(sheet).Cells(row, col + 0).Value = "id"
         Worksheets(sheet).Cells(row, col + 1).Value = "name"
         row = row + 1
      End If

      limit = 100
      For page = 0 To (total_count / limit + 1)
         .Open "GET", url & "projects.xml?limit=100&offset=" & (limit * page), False
         .setRequestHeader "Content-Type", "text/xml"
         .setRequestHeader "X-Redmine-API-Key", key
         .send
         If .Status = 200 Then
            'XMLデータを取り込む
            doc.LoadXML (.responseText)
            Dim node As IXMLDOMNode
            For Each node In doc.SelectSingleNode("projects").ChildNodes
               Worksheets(sheet).Cells(row, col + 0).Value = node.SelectSingleNode("id").Text
               Worksheets(sheet).Cells(row, col + 1).Value = node.SelectSingleNode("name").Text
               row = row + 1
            Next
         Else
            MsgBox .responseText
            GoTo Finally
         End If
      Next
   End With

Finally:
   Set xmlHttp = Nothing
   Set doc = Nothing
End Function

Public Function GetTrackers(ByVal sheet As String, _
                            ByVal row As Long, _
                            ByVal col As Long, _
                            ByVal url As String, _
                            ByVal key As String, _
                            Optional hasHeader As Boolean = false)
'-------------------------------------------------------------------------------
' トラッカーの情報をsheetの(row,col)を起点にして出力
'-------------------------------------------------------------------------------
   Dim total_count As Long
   Dim xmlHttp As New MSXML2.XMLHTTP60
   Dim doc As New DOMDocument60

   If hasHeader Then
      Worksheets(sheet).Cells(row, col + 0).Value = "id"
      Worksheets(sheet).Cells(row, col + 1).Value = "name"
      row = row + 1
   End If

   'trackersにはページネーションがない？
   With xmlHttp
      row = 4 ' 4行目からデータ入力スタート
      .Open "GET", url & "trackers.xml", False
      .setRequestHeader "Content-Type", "text/xml"
      .setRequestHeader "X-Redmine-API-Key", key
      .send
      If .Status = 200 Then
         'XMLデータを取り込む
         doc.LoadXML (.responseText)
         Dim node As IXMLDOMNode
         For Each node In doc.SelectSingleNode("trackers").ChildNodes
            Worksheets(sheet).Cells(row, col + 0).Value = node.SelectSingleNode("id").Text
            Worksheets(sheet).Cells(row, col + 1).Value = node.SelectSingleNode("name").Text
            row = row + 1
         Next
      Else
         MsgBox .responseText
         GoTo Finally
      End If
   End With

Finally:
   Set xmlHttp = Nothing
   Set doc = Nothing
End Function

Public Function GetProjectMenberships(ByVal sheet As String, _
                                      ByVal row As Long, _
                                      ByVal col As Long, _
                                      ByVal url As String, _
                                      ByVal key As String, _
                                      ByVal project_id As String, _
                                      Optional hasHeader As Boolean = false)
'-------------------------------------------------------------------------------
' プロジェクトのメンバーをsheetの(row,col)を起点にして出力
'-------------------------------------------------------------------------------
   Dim total_count As Long
   Dim limit As Long
   Dim page As Long
   Dim xmlHttp As New MSXML2.XMLHTTP60
   Dim doc As New DOMDocument60
   
   With xmlHttp
      'total_countを取得してループ回数決める
      .Open "GET", url & "projects/" & project_id & "/memberships.xml?limit=1", False
      .setRequestHeader "Content-Type", "text/xml"
      .setRequestHeader "X-Redmine-API-Key", key
      .send
      If .Status = 200 Then
         'XMLデータを取り込む
         doc.LoadXML (.responseText)
         total_count = doc.SelectSingleNode("memberships/@total_count").Text
      Else
         MsgBox .responseText
         GoTo Finally
      End If

      If hasHeader Then
         Worksheets(sheet).Cells(row, col + 0).Value = "id"
         Worksheets(sheet).Cells(row, col + 1).Value = "name"
         row = row + 1
      End If

      limit = 100
      For page = 0 To (total_count / limit + 1)
         .Open "GET", url & "projects/" & project_id & "/memberships.xml?limit=100&offset=" & (limit * page), False
         .setRequestHeader "Content-Type", "text/xml"
         .setRequestHeader "X-Redmine-API-Key", key
         .send
         If .Status = 200 Then
            'XMLデータを取り込む
            doc.LoadXML (.responseText)
            Dim node As IXMLDOMNode
            For Each node In doc.SelectSingleNode("memberships").ChildNodes
               Worksheets(sheet).Cells(row, col + 0).Value = node.SelectSingleNode("user/@id").Text
               Worksheets(sheet).Cells(row, col + 1).Value = node.SelectSingleNode("user/@name").Text
               row = row + 1
            Next
         Else
            MsgBox .responseText
            GoTo Finally
         End If
      Next
   End With

Finally:
   Set xmlHttp = Nothing
   Set doc = Nothing
End Function

Function CreateIssues(ByVal sheet As String, _
                      ByVal row As Long, _
                      ByVal col As Long, _
                      ByVal url As String, _
                      ByVal key As String)
'-------------------------------------------------------------------------------
' sheetの(row,col)を起点にチケットの登録
'-------------------------------------------------------------------------------
   Dim issue_id As String
   Dim issue As IssueArgs
   
   Do
      issue_id               = Worksheets(sheet).Cells(row, col + 0).Value
      issue.tracker_id       = Worksheets(sheet).Cells(row, col + 1).Value
      issue.estimated_hours  = Worksheets(sheet).Cells(row, col + 2).Value
      issue.subject          = Worksheets(sheet).Cells(row, col + 3).Value
      issue.description      = Worksheets(sheet).Cells(row, col + 4).Value
      issue.project_id       = Worksheets(sheet).Cells(row, col + 5).Value
      issue.fixed_version_id = Worksheets(sheet).Cells(row, col + 6).Value
      issue.watcher_user_ids = Worksheets(sheet).Cells(row, col + 7).Value

      If issue.subject = "" Then
         Exit Do
      End If

      If issue_id = "" Then
         issue_id = CreateIssue(url, key, issue)
         Worksheets(sheet).Cells(row, col + 0).Value = issue_id
      End If

      row = row + 1
   Loop
End Function

Public Function CreateIssue(ByVal url As String, _
                            ByVal key As String, _
                            ByRef issue As IssueArgs) As String
'-------------------------------------------------------------------------------
' チケットの登録
'-------------------------------------------------------------------------------
    CreateIssue = ""

    Dim xmlDoc   As New MSXML2.DOMDocument60
    Dim xmlPI    As IXMLDOMProcessingInstruction
    Dim rootNode As IXMLDOMNode
    
    'XML宣言(文字コードにはUTF-8を指定)
    Set xmlPI = xmlDoc.appendChild(xmlDoc.createProcessingInstruction("xml", "version=""1.0"" encoding=""UTF-8"""))
    
    Set rootNode = xmlDoc.appendChild(xmlDoc.createNode(NODE_ELEMENT, "issue", ""))

    If Len(Trim(issue.project_id)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "project_id", "")).Text = project_id
    End If
    If Len(Trim(issue.tracker_id)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "tracker_id", "")).Text = tracker_id
    End If
    If Len(Trim(issue.estimated_hours)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "estimated_hours", "")).Text = estimated_hours
    End If
    If Len(Trim(issue.subject)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "subject", "")).Text = subject
    End If
    If Len(Trim(issue.description)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "description", "")).Text = description
    End If
    If Len(Trim(issue.fixed_version_id)) > 0 Then
       rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "fixed_version_id", "")).Text = fixed_version_id
    End If
    If Len(Trim(issue.watcher_user_ids)) > 0 Then
       Dim node As IXMLDOMNode
       Set node = rootNode.appendChild(xmlDoc.createNode(NODE_ELEMENT, "watcher_user_ids", ""))
       node.Attributes.setNamedItem(xmlDoc.createNode(NODE_ATTRIBUTE, "type", "")).NodeValue = "array"

       Dim watchers
       watchers = Split(RemoveSpace(issue.watcher_user_ids), ",")
       For Each watcher_user_id In watchers
          node.appendChild(xmlDoc.createNode(NODE_ELEMENT, "watcher_user_id", "")).Text = watcher_user_id
       Next
    End If

    Debug.Print xmlDoc.XML


     Dim xmlHttp As New MSXML2.XMLHTTP60
     With xmlHttp
         .Open "POST", url & "issues.xml", False
         .setRequestHeader "Content-Type", "text/xml"
         .setRequestHeader "X-Redmine-API-Key", key
         .send xmlDoc.XML
    
         If .Status = 201 Then
             'XMLデータを取り込む
             Dim doc As DOMDocument60
             Set doc = New DOMDocument60
             doc.LoadXML (.responseText)
             CreateIssue = doc.SelectSingleNode("issue/id").Text
         Else
             MsgBox .responseText
         End If
    
     End With
     Set xmlHttp = Nothing

End Function

Function RemoveSpace(ByRef s As String) As String
'-------------------------------------------------------------------------------
' 空白の削除
'-------------------------------------------------------------------------------
   s = Replace(s, " ", "")
   s = Replace(s, "　", "")
   RemoveSpace = s
End Function
